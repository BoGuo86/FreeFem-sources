//  run with MPI:  ff-mpirun -np 4 script.edp
// NBPROC 4

load "PETSc"                        // PETSc plugin
macro trueRestrict()true// EOM
macro removeZeros()true// EOM
macro dimension()2// EOM            // 2D or 3D
include "macro_ddm.idp"             // additional DDM functions

macro def(i)i// EOM                 // scalar field definition
macro init(i)i// EOM                // scalar field initialization
func Pk = P2;                       // finite element space
func Pkdc = P2dc;                   // finite element space

mesh Th;
fespace Vh(Th, Pk);
fespace Ph(Th, Pkdc);
int[int][int] intersection;   // local-to-neighbors renumbering
real[int] D;                  // partition of unity
mesh ThBackup;
{
    Th = square(getARGV("-global", 40), getARGV("-global", 40));
    ThBackup = Th;
    int s = getARGV("-split", 1);
    build(Th, s, intersection, D, Pk, mpiCommWorld)
}

Vh b = 1;
real a = 0.001;
func real f(real u) { return u*a+u-log(1+u); }
func real df(real u) { return a+u/(1+u); }
func real ddf(real u) { return 1/((1+u)*(1+u)); }

Ph alpha;
Ph dfalpha;
Ph ddfalpha;
Vh u = 0;

varf vdJ(uh, vh) = int2d(Th)(dfalpha*(dx(u)*dx(vh) + dy(u)*dy(vh)))
    + on(1, 2, 3, 4, uh = 0);
varf vC(uh, vh) = int2d(Th)(b*vh)
    + on(1, 2, 3, 4, uh = 0);
varf vhJ(uh, vh) = int2d(Th)(dfalpha*(dx(uh)*dx(vh) + dy(uh)*dy(vh)) + ddfalpha*(dx(u)*dx(vh) + dy(u)*dy(vh)) * (dx(u)*dx(uh) + dy(u)*dy(uh)))
    + on(1, 2, 3, 4, uh = 0);

Mat dJ(Vh.ndof, intersection, D);
matrix H;

func real[int] funcRes(real[int]& inPETSc) {
    changeNumbering(dJ, u[], inPETSc, inverse = true, exchange = true);
    alpha = dx(u)*dx(u) + dy(u)*dy(u);
    dfalpha = df(alpha);
    ddfalpha = 2 * ddf(alpha);
    real[int] v = vdJ(0, Vh, tgv = -1);
    real[int] outPETSc;
    changeNumbering(dJ, v, outPETSc);
    return outPETSc;
}
func int funcJ(real[int]& inPETSc) {
    changeNumbering(dJ, u[], inPETSc, inverse = true, exchange = true);
    H = vhJ(Vh, Vh, tgv = -1);
    dJ = H;
    return 0;
}

real[int] xPETSc, bPETSc;
u[] = vC(0, Vh);
changeNumbering(dJ, u[], bPETSc);
xPETSc.resize(bPETSc.n);
xPETSc = 0;
SNESSolve(dJ, funcJ, funcRes, bPETSc, xPETSc, sparams = "-snes_monitor -ksp_monitor_true_residual -snes_max_it 4 -snes_converged_reason -ksp_converged_reason -pc_type lu -snes_view");
changeNumbering(dJ, u[], xPETSc, inverse = true, exchange = false);
fespace VhG(ThBackup, Pk);
VhG uG, uReduce;
uReduce = u;
mpiAllReduce(uReduce[], uG[], mpiCommWorld, mpiSUM);
if(mpirank == 0) {
    plot(uG, cmm = "Global solution", wait = 1, fill = 1, dim = 3);
    ThBackup = adaptmesh(ThBackup, uG);
}
broadcast(processor(0), ThBackup);
uG = uG;
plot(uG, cmm = "Global solution", wait = 1, fill = 1, dim = 3);
{
    Th = ThBackup;
    intersection.resize(0);
    D.resize(0);
    int s = getARGV("-split", 1);
    build(Th, s, intersection, D, Pk, mpiCommWorld)
    Mat dAdapt(Vh.ndof, intersection, D);
    dJ = dAdapt;
}
u = uG;
plotMPI(Th, u[], "Global solution", Pk, def, real, 3, 1)
xPETSc.resize(0);
changeNumbering(dJ, u[], xPETSc);
u[] = vC(0, Vh);
changeNumbering(dJ, u[], bPETSc);
SNESSolve(dJ, funcJ, funcRes, bPETSc, xPETSc, sparams = "-snes_monitor -snes_max_it 40 -ksp_monitor_true_residual -snes_converged_reason -ksp_converged_reason -pc_type lu -snes_view");
changeNumbering(dJ, u[], xPETSc, inverse = true, exchange = true);
plotMPI(Th, u[], "Global solution", Pk, def, real, 3, 1)
